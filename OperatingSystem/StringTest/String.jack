// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents a String object. Implements the String type.
 */
class String {
    field Array string;
    field int length;

    /** Constructs a new empty String with a maximum length of maxLength. */
    constructor String new(int maxLength) {
        let length = 0;
        if (maxLength > 0) {
            let string = Array.new(maxLength);
        }
        return this;
    }

    /** De-allocates the string and frees its space. */
    method void dispose() {
        let length = 0;
        return;
    }

    /** Returns the current length of this String. */
    method int length() {
        return length;
    }

    /** Returns the character at location j. */
    method char charAt(int j) {
        return string[j];
    }

    /** Sets the j'th character of this string to be c. */
    method void setCharAt(int j, char c) {
        let string[j] = c;
        return;
    }

    /** Appends the character c to the end of this String.
     *  Returns this string as the return value. */
    method String appendChar(char c) {
        let string[length] = c;
        let length = length + 1;
        return this;
    }

    /** Erases the last character from this String. */
    method void eraseLastChar() {
        if (length > 0) {
            let length = length - 1;
        }
        return;
    }

    /** Returns the integer value of this String until the first non
     *  numeric character. */
    method int intValue() {
        var int sum, i;
        var boolean neg;
        let sum = 0;
        if(string[0] = 45) {
            let neg = true;
            let i = 1;
        } else {
            let i = 0;
        }
        while (i < length) {
            if ((string[i] > 47) & (string[i] < 58)) {
                let sum = sum * 10 + (string[i] - 48);
                let i = i + 1;
            } else {
                let i = length;
            }
        }
        if (neg) {
            let sum = -sum;
        }
        return sum;
    }

    /** Sets this String to hold a representation of the given number. */
    method void setInt(int number) {
        do dispose();
        if (number < 0) {
            let number = -number;
            do appendChar(45);
        }
        do setDigit(number);
        return;
    }

    method void setDigit(int number) {
        var int d;
        let d = number - ((number / 10) * 10);
        if (~(number < 10)) {
            do setDigit(number / 10);
        }
        do appendChar(d + 48);
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
